#class {necropowers} {open}

/* 
	V1.0
	Edit this list in descending order of priority (1 = highest) for studyall.
	A Qty over 100 will be studied in blocks of 100 automatically.
	Name is all lowercase and must match exactly what you see 
	power listing. If you the qty memorised (or more) it's skipped.

	If you play multiple necromancers you can create different variables
	based on $ses_name or place it in your the character.tin file.
*/
#variable {powerstudy}
{
	{1} {{name}{protection} {qty}{5}}
    {2} {{name}{animate dead} {qty}{2}}
	{3} {{name}{soulbind} {qty}{5}}
    {4} {{name}{unholy ground} {qty}{5}}
	{5} {{name}{rope trick} {qty}{5}}
    {6} {{name}{shift} {qty}{5}}
    {7} {{name}{will} {qty}{5}}
	{8} {{name}{dispell} {qty}{5}}
	{9} {{name}{turn undead} {qty}{5}}
    {10} {{name}{daemon graft} {qty}{3}}
    {11} {{name}{create light} {qty}{3}}
    {12} {{name}{spiritgrasp} {qty}{2}}   
	{13} {{name}{dream} {qty}{600}}
}

#variable {globes} {0}
#variable {memory} {0}

#action {{^You currently have (\d+)/(\d+) open memory globes}}
{
	#variable {globes[open]} {%2};
	#variable {globes[max]} {%3};
	#delay {0.2} {necstudy}
}

#action {{([a-z][a-z\s]+)\([0-9]\):[\s]+(\d+)[\s]+[\d]+([\s]+([a-z][a-z\s]+)\([0-9]\):[\s]+(\d+))?}}
{
	#variable {memory[%2]} {%3};

	#if {%6}
	{
		#variable {memory[%5]} {%6}
	}
}

#alias {necstudy}
{
	#foreach {*powerstudy[%*]} {index}
	{
		#local {name} {$powerstudy[$index][name]};
		#local {qty} {$powerstudy[$index][qty]};
		#local {tostudy} {0};

		#if {$memory[$name]}
		{
			#if {$memory[$name] < $qty}
			{
				#math {tostudy} {$qty - $memory[$name]}
			} {
				#nop no studying necessary
			}
		} {
			#variable {tostudy} {$qty}
		};

		#while {$tostudy > 0}
		{
			#if {$tostudy > 100}
			{
				#send {study $name 100}
			} {
				#send {study $name $tostudy}
			};
			#math {tostudy} {$tostudy - 100}
		}
	};

	#send {study LASTPOWER};
	#delay {0} {#class {necropowers} {kill}}
}

#class {necropowers} {close}
